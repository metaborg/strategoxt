/**
 * Java-specific functional refinement of standard strategies.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module java-integration

imports
  libstratego-lib

strategies
  override term-address-lt(|t2) =
    ?t1 ; <address-lt>(t1,t2)

  override address-lt =
    (checksum, checksum) ; string-lt ; ![]

  override fatal-err(|msg) =
    log(|Critical(), msg, <id>);
    SRTS-EXT-fatal-err(|msg, <try(concat-strings)>)
  
  override fatal-err-msg(|msg) =
    log(|Critical(), msg);
    SRTS-EXT-fatal-err(|msg, ())
  
  override fatal-error =
    error;
    SRTS-EXT-fatal-err(|<try(concat-strings)>, ())
  
  override report-failure(|message, term) = SRTS-EXT-fatal-err(|message, <try(concat-strings)> term, <id>)
  
  
  /**
  * Raise an execution exception with a stack trace
  * @param String
  * @param Term
  * @param List(String)
  */
  external SRTS-EXT-fatal-err(|msg, term, trace)
  
  /**
  * Same as above except that it retrieves the stack trace itself
  */
  SRTS-EXT-fatal-err(|msg, term) = SRTS-EXT-fatal-err(|msg, term, <prim("SSL_stacktrace_get_all_frame_names",|); Tl; Tl>)

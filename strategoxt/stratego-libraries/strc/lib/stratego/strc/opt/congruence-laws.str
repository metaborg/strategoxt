/**
 * @TODO: Use new abstract syntax for annotations.
 */

module congruence-laws
imports Stratego-Sugar Stratego-Sugar-MetaTransition 
	stratego/strc/front/desugar

rules

  MatchingCongruence :
    Cong(f, ss) -> Match(Anno(Op(f, ts), Wld()))
    where <map(\ Match(t) -> t \ + \ Id() -> Wld() \ )> ss => ts

  MatchingCongruence :
    Cong(f, []) -> Match(Anno(Op(f, []), Wld()))

  TransformingCongruence :
    Cong(f, ss) -> 
    Scope([a | <conc>(xs, ys)],
      Seq(Match(Var(orig)),
        Seq(Match(Anno(Op(f, xs'), Var(a))),
          Seq(<seqs> ss',
            Seq(Build(Var(orig)),
              Build(Anno(Op(f, ys'), Var(a))))))))
    where <map(<newname> "cong_param_m")> ss => xs
        ; <map(!Var(<id>))> xs => xs'
        ; orig := <newname> "where"
        ; a := <newname> "cong_annos"
        ; <map(<newname> "cong_param_b")> ss => ys
        ; <map(!Var(<id>))> ys => ys';
          <zip(\ ((x,y), s) -> 
	         Seq(Build(Var(x)), 
		 Seq(s, Match(Var(y)))) \ )> (<zip(id)>(xs,ys), ss) => ss'

  TransformingAnnoCongruence :
    Call(SVar("Anno_Cong__"), [Cong(f, ss), s]) -> 
    Scope([a, b | <conc>(xs,ys)],
      Seq(Match(Var(orig)),
        Seq(Match(Anno(Op(f, xs'), Var(a))),
          Seq(<seqs> ss',
            Seq(Build(Var(orig)),
              Build(Anno(Op(f, ys'), Var(b))))))))
    where <map(<newname> "cong_param_m")> [s | ss] => [a | xs]; map(!Var(<id>)) => xs';
	  <map(<newname> "cong_param_b")> [s | ss] => [b | ys]; map(!Var(<id>)) => ys';
          <zip(\ ((x,y), s) -> Seq(Build(Var(x)), Seq(s, Match(Var(y)))) \ )> 
            (<zip(id)>(xs,ys), [s | ss]) => ss'
        ; orig := <newname> "where"

/*
  Canon :
    Cong(c, ss) -> 
    Scope(<conc>(xs,ys), 
          Seq(Match(Op(c, <map(\ x -> Var(x)\ )> xs)), 
              Seq(Where(<foldr(!Id,\ (s1,s2) -> Seq(s1,s2) \ )> ss'),
                  Build(Op(c, <map(\ x -> Var(x)\ )> ys)))))
    where <unzip(ApplyStrat)> ss => (xys, ss')
        ; <unzip(id)> xys => (xs, ys)

  ApplyStrat : 
    s -> ((x,y),Seq(Build(Var(x)), Seq(s, Match(Var(y)))))
    where x := <newname> "apply_b"
        ; y := <newname> "apply_m"
*/

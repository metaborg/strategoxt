/**
 * sdf-ensugar implements the following ensugarings:
 *
 *   - add double quotes in String literals: cons, lit
 *   - replace attrs([]) by no-attrs
 *
 * @author Martin Bravenboer <martin.bravenboer@gmail.com>
 */
module sdf-ensugar
imports libstratego-lib libstratego-aterm libstratego-sglr
strategies

  main-sdf-ensugar =
    io-wrap(
      topdown(repeat(Ensugar); try(EnsugarOnce))
    )

  Ensugar :
    attrs([]) -> no-attrs()

  /**
   * A seq where the second element is not a list, is a lookahead seq.
   */
  Ensugar :
    seq(a,b) -> seq-lookahead(a,b)
    where
      <not(is-list)> b

  /**
   * A seq where the second element is a list, is a symbol
   */
  Ensugar :
    seq(a, b) -> seq-symbol(a,b)
    where
      <is-list> b

signature
  constructors
    seq-symbol : a * a -> a
    seq-lookahead : a * a -> a

strategies

  EnsugarOnce :
    lit(s) -> lit(<double-quote> s)
 
 EnsugarOnce :
    ci-lit(s) -> ci-lit(<single-quote> s)
 
  EnsugarOnce =
    term(explode-aterm)


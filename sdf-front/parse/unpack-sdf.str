/**
 * Unpack-sdf extracts the modules from an SDF definition and writes
 * them to separate files.
 *
 * @author  Rob Vermaas
 * @author  Martin Bravenboer
 */
module unpack-sdf
imports lib xtc-lib sdf-xtc-tools Sdf2

strategies

  main-unpack-sdf =
    xtc-io-wrap(unpack-sdf-options, unpack-sdf)

  unpack-sdf-options =
    ArgOption("-d", where(<set-config>("-d", <id>)), !"-d dir    Destination directory")

  get-destination-dir =
    <get-config> "-d" <+ !"."

strategies

  unpack-sdf =
      xtc-parse-sdf2-definition
    ; read-from
    ; split-sdf-definition
    ; <xtc-exit> 0

  split-sdf-definition =
    definition(map(save-sdf-module-to-file))

  save-sdf-module-to-file =
    where(get-module-name => basename)
  ; xtc-temp-files(
      write-to
    ; xtc-pp-sdf2
    ; where( 
        <concat-strings>[<get-destination-dir>, "/", basename, ".sdf"] => pathname
      ; dirname
      ; mkdir
      )
    ; copy-to(!pathname)
    )

  get-module-name =
    ?'module(<module-name-to-string>,_,_)

  module-name-to-string :
    unparameterized(s) -> s

  module-name-to-string :
    parameterized(s, _) -> s

strategies

  mkdir = 
    where(readdir <+ <call> ("mkdir",["-p", <id>]))

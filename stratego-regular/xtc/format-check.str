module format-check
imports 
  libstratego-lib
  libstratego-xtc
  libstratego-rtg
  strategoxt-xtc-tools
  tool-doc
  regular-options
  regular-xtc-tools
  core-format-check-pp-aterm
  core-format-check-to-xhtml

/**
 * Main program
 */
strategies

  main-format-check =
    xtc-io-wrap(
      fc-options
    , fc-usage
    , fc-about 
    , ![]
    , read-from
      ; select-format-check
      ; xtc-output(id)
      ; get-exit-code
      ; xtc-exit
    )
  
  select-format-check =
    let construct-start-nonterms =
          get-start-nonterm; ![Nonterm(<id>)]
          <+ ?RTG(Start(<id>), _)

    in where(
         rtg := <input-to-rtg-nf>
       ; starts := <construct-start-nonterms> rtg
       )
     ; if is-xhtml-mode then
        format-check-xhtml(|rtg, starts)
      else
        if is-vis-mode then
          format-check-pp-aterm(|rtg, starts)
        else
          format-check-plain(|rtg, starts)
          ; if <get-config> "-b" then write-to else write-to-text end
        end
      end
   end

  get-exit-code =
    <get-config> "--exit-code" <+ !0


strategies

  format-check-plain(|rtg, starts) =
    where(
      if-fast-mode(
        topdown(
          try(is-string; !"")
        ; try(is-int; !0)
        )
      )
    ; rtg-format-check(true, true | rtg, starts)
    ; <set-config> ("--exit-code", 0)
    )
    <+ <xtc-exit> 1

  format-check-xhtml(|rtg, starts) =
    core-format-check-xhtml(|rtg, starts)
    ; write-to
    ; xtc-transform(!"pp-xml-doc", pass-verbose)

  format-check-pp-aterm(|rtg, starts) =
    core-format-check-pp-aterm(|rtg, starts)
    ; write-to
    ; xtc-abox2text


/**
 * Options
 */
strategies

  fc-options =
         rtg-option(|"Format check against rtg in file")
    + rtg-nf-option(|"Format check against normalized rtg in file")
    + start-nonterm-option(|"Term must be of type s (default: start non-terms of rtg)")
    + xhtml-option
    + vis-option
    + fast-option(|"Run as fast as possible. Might reduce quality of error messages.")

  xhtml-option =
    Option("--xhtml"
    , set-xhtml-mode
    , !HelpString("--xhtml", "Present the result in XHTML")
    )

  is-xhtml-mode =
    <get-config> "--xhtml"

  set-xhtml-mode =
    <set-config> ("--xhtml", ())

  vis-option =
    Option("--vis"
    , set-vis-mode
    , !HelpString("--vis", "Visualize in colored pp-aterm")
    )

  is-vis-mode =
    <get-config> "--vis"

  set-vis-mode =
    <set-config> ("--vis", ())

strategies

  fc-usage =
    <tool-doc>
    [ Usage("format-check --rtg file [OPTIONS]")
    , Summary(
       "
format-check analyzes whether the input ATerm conforms to the format
that is specified in the RTG (Regular Tree Grammar).
	"
      )
    , OptionUsage()
    , Description(["
format-check verifies that the input ATerm is part of the language
defined in the RTG.  If this is not the case, then the ATerm contains
format errors. format-check can operate in three modes: plain,
visualize and XHTML.
	","
The plain mode is used if the other modes are not enabled. In the
plain mode format errors are reported and no result is written the the
output (stdout or a file). Hence, if format-check is included in a
pipeline, then the following tool will probably fail. If the input
term is correct, then it is written to the output.
	","
The visualize mode is enabled with the --vis option. In visualize mode
format errors are reported and in a pretty-printed ATerm will be
written to the output. All innermost parts of the ATerm that cause
format errors are printed in red, if your terminal supports control
characters for colors. If you want to browse through the ATerm with
less, then you should use the -r flag.
	","
The XHTML mode is enabled with the --xhtml option. In XHTML mode
format errors are reported and an report in XHTML will be written to
the output. This report shows the parts of the ATerm that are not
formatted correctly. Also, moving with your mouse over the nodes of
ATerm, will show the non-terminals that have be inferred by
format-check (do not use IE6. Firefox or Mozilla is recommended).
	","
format-check reports all innermost format errors. That is, only the
deepest format errors are reported. A format error is reported by
showing the ATerm that is not in the correct format, and the inferred
types of the children of the ATerm. In XHTML and visualize mode a
format error of term in a list is presented by a red comma and
term. This means that a type has been inferred for the term itself,
but that it is not expected at this point in the list. If only the
term is red, then no type could be inferred for the term itself.
	","
In all modes format-check succeeds (exit code 0) if the ATerm contains
no format errors. If the term does contain format errors, then
format-check fails (exit code 1).
	"])
    , AutoReportBugs()
    ]

  fc-about =
    <tool-doc>
      [ AutoProgram()
      , Author(Person("Martin Bravenboer", "martin@cs.uu.nl"))
      , GNU_LGPL("2002-2006", "Martin Bravenboer <martin@cs.uu.nl>")
      , WebHome("http://www.program-transformation.org/Tools/FormatCheck")
      , Config([
          DefaultXTCRepository()
        , CurrentXTCRepository()
        ]) 
      ]

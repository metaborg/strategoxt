/**
 * Overrides some of the built-in, recusively defined libstratego-lib
 * strategies for better stack behavior and performance.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module performance-tweaks

imports libstratego-lib

signature constructors
  Cons   : Term * List -> List
  Nil    : List
  Stream : Int -> Stream

strategies

  override map(s) = ?[_ | _] < all(s) + []
  
  override getfirst(s) = ?[_ | _]; one(where(s; ?x)); !x
  
  override fetch-elem(s) = getfirst(s)
  
  override fetch(s) =  ?[_ | _]; one(s)
  
  override list-loop(s) = ?[_ | _] < all(where(s)) + []
  
  override read-text-from-stream =
    ?Stream(stream);
    prim("SSL_EXT_read_text_from_stream", stream)
  
  override crush(nul, sum) =
    SRTS-EXT-crush(nul, sum, id)
  
  override crush(nul, sum, s) =
    SRTS-EXT-crush(nul, sum, s)

  override foldr(nul, sum) =
    ?[_ | _] < SRTS-EXT-crush(nul, sum, id) + []; nul

  override foldr(nul, sum, s) =
    ?[_ | _] < SRTS-EXT-crush(nul, sum, s) + []; nul
  
  override repeat(s) =
    SRTS-EXT-repeat(s)
  
  override filter(s) =
    SRTS-EXT-filter(s)
  
  override at-end(s) =
    SRTS-EXT-at-end(s)

  external SRTS-EXT-crush(nul, sum, s|)
  
  external SRTS-EXT-filter(s|)

  external SRTS-EXT-at-end(s|)

  external SRTS-EXT-repeat(s|)

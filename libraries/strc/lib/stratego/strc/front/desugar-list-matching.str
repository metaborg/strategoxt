/**
 * Desugar list matching.
 */

module desugar-list-matching

imports Stratego-Sugar 
	stratego/strc/front/desugar 
	stratego/strc/lib/stratlib

strategies

  desugar-list-matching = 
    rec dlm(
      alltd(
           ?SDefT(_,_,_,_); ListVarScope; all(dlm)
        <+ ?Scope(_, _); ListVarScope; all(dlm)
        <+ RtoS; all(dlm)
        <+ Build(dlm-in-build(dlm))
        <+ CallT(id, map(dlm), map(dlm-in-build(dlm)))
	<+ CallDynamic(dlm-in-build(dlm), map(dlm), map(dlm-in-build(dlm)))
        <+ Match(dlm-in-match(dlm))
      )
    )

  dlm-in-build(dlm) =
    alltd(
      ListVarAsTail; dlm-in-build(dlm)
      <+ ListVarInBuild; dlm-in-build(dlm)
      <+ ConcInBuild; dlm-in-build(dlm)
      <+ SingleListVar
      <+ ConcInBuild
      <+ App(dlm, dlm-in-build(dlm))
      <+ RootApp(dlm)
    )

  dlm-in-match(dlm) = 
    alltd(
      ListVarAsTail; dlm-in-match(dlm)
      <+ SingleListVar
      //<+ ConcInMatch
      <+ App(dlm, dlm-in-match(dlm))
      <+ RootApp(dlm)
    )

rules

  ListVarScope :
    Scope(xs, s) -> Scope(ys, s)
    where <map(try(?ListVar(<id>)))> xs => ys

  ListVarScope :
    SDefT(f, as1, as2, s) -> SDefT(f, as1, as2', s)
    where <map(try(DefaultVarDec(?ListVar(<id>)) + VarDec(?ListVar(<id>),id)))> as2 => as2'
	//; <concat-strings> [f, "(", <length;int-to-string> as1, ",", <length; int-to-string> as2, ")"] => name
	//; rules(CurrentDef : _ -> name )

  SingleListVar :
    Var(ListVar(x)) -> Var(x)

  is-nil-trm = 
    ?Term|[ Nil() ]| 
    + ?Term|[ Nil(){^pt} ]| 
    + ?PreTerm|[ Nil() ]|

  ListVarAsTail : 
    Term|[ Cons(lid, t){^pt} ]| -> Term|[ lid{^pt} ]|
    where <is-nil-trm> t

  ListVarAsTail : 
    PreTerm|[ Cons(lid, t) ]| -> PreTerm|[ lid ]|
    where <is-nil-trm> t

  ListVarInBuild : 
    Term|[ Cons(lid, t){^pt} ]| -> <desugar>Term|[ <makeConc(|)>(lid, t){^pt} ]|
    where <not(is-nil-trm)> t

  ConcInMatch =
    ?Term|[ Conc(t1, t2){^pt} ]|
    // ; err(|<concat-strings>["concatentation in match in definition ", <CurrentDef>])

  ConcInBuild :
    Term|[ Conc(t1, t2){^pt} ]| -> <desugar>Term|[ <makeConc(|)>(t1, t2){^pt} ]|

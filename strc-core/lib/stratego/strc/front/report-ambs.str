module report-ambs
imports 
  libstratego-lib
  libstratego-xtc
  Stratego-Sugar

signature
  constructors
    amb : List(Term) -> Term

strategies

  report-ambs =
    report-amb
    <+ definition(report-ambs)
    <+ all(report-ambs)

  definition(s) =
    (?SDef(name,_,_) <+ ?SDefNoArgs(name,_) <+ ?SDefT(name,_,_,_))
    ; {| current-definition
       : rules( current-definition : _ -> ("strategy", name) )
       ; all(s)
       |}
 
  definition(s) =
    (?RDef(name,_,_) <+ ?RDefNoArgs(name,_) <+ ?RDefT(name,_,_,_))
    ; {| current-definition
       : rules( current-definition : _ -> ("strategy", name) )
       ; all(s)
       |}

  report-amb =
    ?amb(_) 
    ; where( current-definition => (type, name) )
    ; xtc-temp-files( 
        write-to
        ; xtc-transform(!"pp-stratego",!["--abstract"])
        ; FILE(read-text-file => pp)
      )
    ; log(|Error(),["Ambiguity found in ",type, " ",name])
    ; say(!pp) 
 

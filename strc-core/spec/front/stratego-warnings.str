/**
 * Tool for generating stratego compilation warnings 
 *
 * @todo The Stratego-warnings strategy is very expensive since it does a collect
 *       for each kind of warning. Better is to do a single traversal, possibly
 *       using a dynamic rule to turn on or off certain warnings. The warning can
 *       be printed immediately, or otherwise collected using a dynamic rule.
 *
 * @todo Passing the spec as a strategy argument is bad style; spec should be the current term. 
 *
 * @author Merijn de Jonge (m.de.jonge@tue.nl)
 */
module stratego-warnings
imports
  libstratego-xtc
  libstrc

strategies

  stratego-warnings-io = 
    io-wrap(stratego-warnings-options, Stratego-warnings)

imports Stratego-Sugar libstrc

overlays

  AllWarnings = [
     ("debug-arguments", "        missing build operator", "on"), 
     ("obsolete-strategy-calls", "obsolete strategies", "on"), 
     ("maybe-unbound-variables", "unbound variables", "off"),
     ("lower-case-constructors", "lower-case constructors", "on")
     ]

strategies

  stratego-warnings-options = 
    ArgOption("--warning" + "-W"
    , switch id
        case "all"  :
          <set-config> ("--warning", <map(Fst)> AllWarnings)

        case explode-string => ['n', 'o', '-' | wchars] :
          <implode-string> wchars => wstring 
          ; warning-exists
          ; <get-config <+ get-default-warnings> "--warning"
          ; filter(not(?wstring))
          ; <set-config> ("--warning", <id>)

        case <elem> (<id>,  <get-all-warnings>) :
          <extend-config> ("--warning", [<id>])

        otherwise :
          <fatal-error> [<get-config> "program", ": ", <id>, ": no such warning"]
      end
    , <concat-strings>
        [ "-W,--warning C   Report warnings falling in category C. Categories:\n",
       "                      all                      all categories \n",
       "                      no-C                     no warnings in category C\n"
        | <Display-possible-warnings> AllWarnings() ]
    )

  get-default-warnings =
    <filter(\ (warning,_,"on") -> warning \)>AllWarnings

  get-all-warnings =
    <map(Fst)> AllWarnings
    
  warning-exists = 
    ?warning ; <fetch-elem(?(warning,_,_))>AllWarnings

  Display-possible-warnings = 
     map( \ (s,txt,df) -> <concat-strings> ["                      ",s, "  ", txt, " [ ",df," ]\n"] \ )

  get-warnings =
    <get-config> "--warning" <+ !AllWarnings(); map(Fst)
    
  pass-warnings = 
    get-warnings
    ; mapconcat(!["--warning", <id>])


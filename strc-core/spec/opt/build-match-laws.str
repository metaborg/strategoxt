module build-match-laws
imports Stratego Stratego-MetaTransition

rules // combinations of match and build

  BuildMatch = 
    MisMatch + BuildMatchFusion + BuildBuild + BuildPrim
    + MatchIdem + BuildMatchIdem + MatchBuildIdemVar
	// + MatchBuildIdem
    + BuildAlwaysSucceeds


  MisMatch :
    Seq(Build(Anno(Op(f, ts1),pt1)), Match(Anno(Op(g, ts2),pt2))) -> Fail
    where <not(eq)> (f, g)

  MisMatch :
    Seq(Build(Anno(Op(f, ts1),p1)), Seq(Match(Anno(Op(g, ts2),pt2)), _)) -> Fail
    where <not(eq)> (f, g)


  BuildMatchFusion :
    Seq(Build(Anno(Op(f, ts1),pt1)), Match(Anno(Op(f, ts2),pt2))) ->
    Seq(<zip(\ (t1,t2) -> Seq(Build(t1), Match(t2)) \ ); seqs> (ts1, ts2), 
    Seq(Build(Anno(pt1,Op(Nil,[]))), 
    Seq(Match(Anno(pt2,Wld)),
        Build(Op(f, ts1)))))

  BuildMatchFusion :
    Seq(Build(Anno(Op(f, ts1),pt1)), Seq(Match(Anno(Op(f, ts2),pt2)), s)) ->
    Seq(<zip(\ (t1,t2) -> Seq(Build(t1), Match(t2)) \ ); seqs> (ts1, ts2), 
    Seq(Build(Anno(pt1,Op(Nil,[]))), 
    Seq(Match(Anno(pt2,Wld)),
    Seq(Build(Op(f, ts1)),
        s))))

  BuildBuild :
    Seq(Build(t1), Build(t2)) -> Build(t2)

  BuildBuild :
    Seq(Build(t1), Seq(Build(t2), s)) -> Seq(Build(t2), s)


  BuildPrim :
    Seq(Build(t1), PrimT(f, ss, ts)) -> PrimT(f, ss, ts)

  BuildPrim :
    Seq(Build(t1), Seq(PrimT(f, ss, ts), s)) -> Seq(PrimT(f, ss, ts), s)


  MatchIdem :
    Seq(Match(t), Match(t)) -> Match(t)

  MatchIdem :
    Seq(Match(t), Seq(Match(t), s)) -> Seq(Match(t), s)
    
  BuildMatchIdem :
    Seq(Build(t), Match(t)) -> Build(t)
    
  BuildMatchIdem :
    Seq(Build(t), Seq(Match(t), s)) -> Seq(Build(t), s)
    
// not valid as long as annotations are not taken into account
//  MatchBuildIdem :
//    Seq(Match(t), Build(t)) -> Match(t)
    
  MatchBuildIdemVar :
    Seq(Match(Var(x)), Build(Var(x))) -> Match(Var(x))

  MatchBuildIdemVar :
    Seq(Match(Var(x)), Seq(Build(Var(x)), s)) -> Seq(Match(Var(x)), s)


rules // other rules 

  MatchSimplify = 
    AsMatchWld

  MatchAs :
    Match(As(p1, p2)) -> Seq(Match(p2), Match(p1))

  AsMatchWld :
    As(Wld, t) -> t

  WhereSavesCurrentTerm :
    Where(s) -> Scope([x], Seq(Match(Var(x)), Seq(s, Build(Var(x)))))
    where new => x

  TestSavesCurrentTerm :
    Test(s) -> Scope([x], Seq(Match(Var(x)), Seq(s, Build(Var(x)))))
    where new => x

rules

  BuildAlwaysSucceeds :
    LChoice(Build(t), s) -> Build(t)
    where <topdown(?Var(_) < ?Var(_){"bound"} 
                   + ?Op("Cons", _) < fail 
                   + id)> t

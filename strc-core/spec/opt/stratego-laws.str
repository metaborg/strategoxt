// This module specifies basic simplification rules for strategies.

module stratego-laws

imports 
  liblib 
  Stratego-Sugar
  desugar  
  scope-laws  
  bind-laws 
  idfail-laws
  congruence-laws 
  build-match-laws

strategies

  Simplify = 
    ElimId
    + ElimFail 
    + AssociateR
    + Idempotency
    + EmptyScope
    + LetNoDefs
    + LetHoist
    //+ BuildMatch
    //+ MatchSimplify

  DefineCongruences =
    (MatchingCongruence <+ TransformingAnnoCongruence <+ TransformingCongruence)

  Optimize =
    Simplify
    + HoistLet 
    + FuseScope
    + WidenScope
    + WhereSavesCurrentTerm
    + TestSavesCurrentTerm
    + LeftChoiceIsSugar
    + ChoiceIsSugar 
    + NotIsSugar
    + RecIsSugar


  simplify0 = 
    downup(repeat(Simplify))

  simplify = 
    innermost(Optimize)

  simplify-widen = // note same as simplify
    simplify

  simplify-narrow =
    topdown(try(NarrowScope))

  simplify-clean =
    topdown(try(CleanupScope; try(FuseScope)))

  lift-term-args =
    topdown(try(LiftPrimArgs + LiftCallArgs))

rules

  LeftChoiceIsSugar :
    |[ s1 <+ s2 ]| -> |[ s1 < id + s2 ]|

  ChoiceIsSugar :
    |[ s1 + s2 ]| -> |[ s1 < id + s2 ]|

  NotIsSugar :
    |[ not(s) ]| -> |[ s < fail + id ]|

  RecIsSugar :
    |[ rec x(s) ]| -> |[ let x(|) = s in x(|) end ]|

rules

  Commutativity : 
    Choice(x, y) -> Choice(y, x)

  AssociateR: 
    Choice(Choice(x, y), z) -> Choice(x, Choice(y, z))

  AssociateR : 
    Seq(Seq(x, y), z) -> Seq(x, Seq(y, z))

  AssociateR : 
    LChoice(LChoice(x, y), z) -> LChoice(x, LChoice(y, z))
 
  AssociateL: 
    Seq(s1, Seq(s2, s3)) -> Seq(Seq(s1, s2), s3)

  Idempotency :
    Choice(x, x)  -> x
  Idempotency :
    LChoice(x, x) -> x
  Idempotency :
    Where(Where(s))  -> Where(s)
  Idempotency :
    Not(Not(s)) -> Test(s)
  Idempotency :
    Test(Test(s)) -> Test(s)
  Idempotency :
    Where(Seq(Where(s1), Seq(Build(t), s2))) ->
    Where(Seq(s1, Seq(Build(t), s2)))
\end{code}

% Copyright (C) 1998-2004 Eelco Visser <visser@acm.org>

